# Copyright 2011-2012, Qualcomm Innovation Center, Inc.
# 
#    Licensed under the Apache License, Version 2.0 (the "License");
#    you may not use this file except in compliance with the License.
#    You may obtain a copy of the License at
# 
#        http://www.apache.org/licenses/LICENSE-2.0
# 
#    Unless required by applicable law or agreed to in writing, software
#    distributed under the License is distributed on an "AS IS" BASIS,
#    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#    See the License for the specific language governing permissions and
#    limitations under the License.
#
import os

Import('env')

if(not(env.has_key('GTEST_DIR'))):
    print('GTEST_DIR not specified skipping common unit test build')
else:
    
    gtest_env = env.Clone();
    gtest_dir = gtest_env['GTEST_DIR']
    vars = Variables();
    vars.AddVariables(('GTEST_HOME', '', gtest_dir))
    vars.Update(gtest_env)
    if(env['OS_GROUP'] == 'windows'):
        # gTest does not require the same CPPDEFINES as AllJoyn core.
        gtest_env.Append(CPPDEFINES = ['WIN32', '_LIB'])
        # don't use the _DEBUG define unless the /MDd compiler flag is specified
        #gtest_env.Append(CPPDEFINES = ['WIN32', '_DEBUG', '_LIB'])
    
    # tr1::tuple is not avalible for android or darwin
    if(env['OS_CONF'] == 'android' or env['OS_CONF'] == 'darwin'):
        gtest_env.Append(CPPDEFINES = ['GTEST_HAS_TR1_TUPLE=0'])
    #we compile with no rtti and we are not using exceptions. 
    gtest_env.Append(CPPDEFINES = ['_HAS_EXCEPTIONS=0', 'GTEST_HAS_RTTI=0'])
    # we replace AllJoyn's include CPPPATH options.  AllJoyn includes stlport that will cause the 
    # gTest code to not compile as expected at this time
    gtest_env.Replace(CPPPATH = [gtest_env.Dir('$GTEST_DIR'), gtest_env.Dir('$GTEST_DIR/include')])    
    if(env['OS_CONF'] == 'android'):
        # Determine Android NDK version
        if os.path.exists(env.subst('$ANDROID_NDK/toolchains')):
            ndkVersion = 5
        else:
            print 'common unit test build requires NDK v5 or newer'
        # Android include paths
        if ndkVersion >= 5:
            gtest_env.Append(CPPPATH=['$ANDROID_NDK/platforms/android-$ANDROID_API_LEVEL/arch-arm/usr/include',
                                      '$ANDROID_NDK/sources/cxx-stl/stlport/stlport'])
    gtest_obj = gtest_env.StaticObject(target = 'gtest-all', source = ['$GTEST_DIR/src/gtest-all.cc'])
    gtest_env.StaticLibrary(target = 'gtest', source = gtest_obj)
    
    #tests currently don't need to compile gtest main.
    #gtest_main_obj = gtest_env.StaticObject(target = '$OBJDIR/gtest_main', source = ['$GTEST_DIR/src/gtest_main.cc'])
    #gtest_main_lib = gtest_env.StaticLibrary(target = '$OBJDIR/gtest_main', source = gtest_main_obj)
#    VariantDir('../$OBJDIR/unittest/public', 'public', duplicate=0)
    #env.SConscript('public/SConscript', variant_dir='../$OBJDIR/unittest/public', duplicate=0)
    #env.SConscript('internal/SConscript', variant_dir='../$OBJDIR/unittest/internal', duplicate=0)
    
    
    test_src = env.Glob('*.cc')

    unittest_env = env.Clone()

    gtest_dir = unittest_env['GTEST_DIR']
    # use the relative path from the build directory.
    # include all header files even if they will not be in the public repository.  
    unittest_env.Replace(CPPPATH = [gtest_dir + '/include', 
                                '../../../../../../inc',
                                '../../../../../../../alljoyn_core/inc'])
    if(env['OS_CONF'] == 'android'):
        # Determine Android NDK version
        if os.path.exists(env.subst('$ANDROID_NDK/toolchains')):
            ndkVersion = 5
        else:
            print 'Cannot determine Android NDK version'
        # Android include paths
        if ndkVersion >= 5:
            unittest_env.Append(CPPPATH=['$ANDROID_NDK/platforms/android-$ANDROID_API_LEVEL/arch-arm/usr/include',
                                '$ANDROID_NDK/sources/cxx-stl/stlport/stlport',
                                '$ANDROID_SRC/external/bluetooth/bluez/include',
                                '$ANDROID_SRC/external/openssl/include'])

    #we compile with no rtti and we are not using exceptions.
    unittest_env.Append(CPPDEFINES = ['_HAS_EXCEPTIONS=0', 'GTEST_HAS_RTTI=0'])
    
    if(env['OS_CONF'] == 'android' or env['OS_CONF'] == 'darwin'):
        unittest_env.Append(CPPDEFINES = ['GTEST_HAS_TR1_TUPLE=0'])
    # path for alljoyn library file
    unittest_env.Append(LIBPATH = ['$DISTDIR/lib'])
    # gtest library file is placed in same folder as the the object files.
    unittest_env.Append(LIBPATH = ['./'])
    #the linker must be able to find stlport for android
    if(env['OS_CONF'] == 'android'):
        unittest_env.Append(LIBPATH = ['$ANDROID_NDK/platforms/android-$ANDROID_API_LEVEL/arch-arm/usr/lib'])
    
    # using the alljoyn library to test common since the status object file is not
    # compiled till alljoyn_core is compiled.
    unittest_env.Prepend(LIBS = ['gtest', 'alljoyn'])
    
    obj = unittest_env.Object(test_src);
    # statically link the stlport lib
    if(env['OS_CONF'] == 'android'):
        obj.append('$ANDROID_NDK/sources/cxx-stl/stlport/libs/armeabi/libstlport_static.a')

    unittest_prog = unittest_env.Program('cmtest', obj)
    unittest_env.Install('$TESTDIR/bin', unittest_prog)
